#
################################################################################
#
# kpar_par.mac
#
################################################################################
#
# Displays the correlation between two parameter values. The member with the
# lowest R-value is plotted in blue, all other ones in red.
# at the bottom of the plot the current trial values are plotted.
#
# @kpar_par generation, parameter, parameter
#       generation may be any generation number or -1 for the current one
#       parameter: -1 == member number
#       parameter:  0 == R-value
#       parameter: >0 == parameter nuumber
#
################################################################################
#
variable integer,generation
variable integer,member
variable integer,children
variable integer,parameters
variable integer,kid
fclose all
#
rese
#
# Get generation numbers, number of members, children and parameters
#
fopen 1,GENERATION
#
fget  1,generation,member,children,parameters
fclose 1
#
if($1.eq.-1) then
  i[0] = generation - 1
else
  i[0] = $1
endif
#
# Find member with lowest R-value
#
rese
load st, DIFFEV/Parameter, i[0],$2 +2,   2
r[0] =y[1,1]
i[10]=1
do i[1]=2,np[1]
  if(y[1,i[1]].lt.r[0]) then
    r[0] = y[1,i[1]]
    i[10] = i[1]
  endif
enddo
#
#  load actual parameter sets
#
rese
load st, DIFFEV/Parameter, i[0],$2 +2,$3+2
alloc minimum,1
x[2,1] = x[1,i[10]]
y[2,1] = y[1,i[10]]
#
tit1 Correlation between parameters
if($1.eq.-1) then
  i[0] = generation - 1
else
  i[0] = $1
endif
#
# create pretty plot
#
tit2 "Generation %4d; Parameter Nr. %d / %d",i[0],$2,$3
#
ltyp 1,0
mtyp 1,3
lcol 1,1
mcol 1,1
#
r[0] = (ymax[1]-ymin[1])*0.05
r[1] = ymin[1]-r[0]
#
#
ltyp 1,0
mtyp 1,3
lcol 1,1
mcol 1,1
#
ltyp 2,0
mtyp 2,3
lcol 2,3
mcol 2,3
#
if($2.gt.0) then
#
  alloc trials,children
  do i[0]=1,children
    fopen 1, "DIFFEV/Trials.%4D",i[0]
    fget  1
    fget  1
    fget  1
    fget  1
    fget  1
    do i[1] = 1, $2-1
      fget 1
    enddo
    fget 1, x[n[1],i[0]]
    y[n[1],i[0]] = ymin[1] - r[0]
    fclose 1
  enddo
  r[10] = min(xmin[1],xmin[3])
  r[11] = max(xmax[1],xmax[3])
#
  ltyp 3,0
  mtyp 3,3
  lcol 3,3
  mcol 3,6
#
  kfra 1,1,2,3
else
  r[10] = xmin[1]
  r[11] = xmax[1]
  kfra 1,1,2
endif
#
skal xmin[1],xmax[1],ymin[1]-r[0],ymax[1]+r[0]
mark
fnam off
#
plot
show data,2
set prompt,on
